#!/bin/csh -f

# Check for enough arguments
set argc="$#argv"
if ("$argc" < 3) then
  echo "Usage:  update basename command_type command"
  exit 1
endif

set cmd_dir=`dirname $0`

set basename="$argv[1]"
set command_type="$argv[2]"

# Create an empty unknown file if there isn't one
if (! -e "$basename.unknown") then
  touch $basename.unknown
endif

# Create an empty undecided file if there isn't one
if (! -e "$basename.undecided") then
  touch $basename.undecided
endif

# Find log file (if it exists) and set the current log number
set logfile="$basename.log"
if (-e "$logfile" && ! -z "logfile") then
  set log_number=`tail -1 "$logfile" | awk '{print $1 + 1;}'`
else
  set log_number=1
  echo "Integrate_Data.py $basename.out $basename.halt $basename.infinite $basename.unknown $basename.error $basename.undecided " >> "$logfile"
endif

# Get the original number of unknown and undecided TMs
set orig_unknown=`wc -l < $basename.unknown`
set orig_undecided=`wc -l < $basename.undecided`

# Get the start date and command
set start_date=`\date`
set command="$argv[3-] --infile=$basename.unknown --outfile=$basename.out --log_number=$log_number"

# Run the command
echo "Running: $command"
$command

# Check for failure
if ("$status" != 0) then
  echo "Command failed"
  exit 1
endif

# Save the initial output file
if ("$argv[2]" == "Generate.py") then
  cp -f "$basename.out" "$basename.all"
endif

# Log the running of the command
echo "$log_number" "$command_type" | awk '{printf("%6d %s",$1,$2);}' >> "$basename.log"

# Check for any change
cmp -s "$basename.unknown" "$basename.out"
if ("$status" != 0) then
  # Integrate the output data into the existing database and get TM counts
  set counts=`Integrate_Data.py "$basename.out" "$basename.halt" "$basename.infinite" "$basename.unknown" "$basename.error" "$basename.undecided"`

  # Check for failure
  if ("$status" != 0) then
    echo "Integration failed"
    exit 1
  endif

  # Break out the individual TM counts
  set count_halt=$counts[1]
  set count_infinite=$counts[2]
  set count_undecided=$counts[3]
  set count_unknown=$counts[4]
  set count_error=$counts[5]

  # Get the net change in unknown TMs
  set net_undecided=`expr $count_undecided - $orig_undecided`
  set net_unknown=`expr $count_unknown - $orig_unknown`

  # Get the end date
  set end_date=`\date`
  set delta_time=`Diff_Date.py "$start_date" "$end_date"`

  # Log the command and print a bit more output
  echo " | $command | $start_date | $delta_time | $count_halt $count_infinite $net_undecided $net_unknown" >> "$basename.log"
  echo "TM counts: $count_halt $count_infinite $net_undecided $net_unknown"

  # Log if all the TMs have been resolved
  set count_un=`cat $basename.unknown $basename.undecided | wc -l`
  if ("$count_un" == "0") then
    echo "------ | All TMs resolved" >> "$basename.log"    
    echo "All TMs resolved"
  endif
else
  rm -f "$basename.out"

  # Get the end date
  set end_date=`\date`
  set delta_time=`Diff_Date.py "$start_date" "$end_date"`

  # Log the command and indicate no change
  echo " | $command | $start_date | $delta_time | 0 0 0 0" >> "$basename.log"
  echo "TM counts: 0 0 0 0"
endif

# Exit successfully
exit 0
